<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.38">
  <POU Name="FB_SerialCom" Id="{181e7c70-138b-446a-a6f5-34e588388885}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_SerialCom
VAR_IN_OUT
	TxBuffer	: ComBuffer;
	RxBuffer	: ComBuffer;
END_VAR
VAR
	Timer		: TON := (PT:=T#1S);
	fbSend		: SendString;
	bSendBusy	: BOOL;
	eSendErrorID: ComError_t;

	fbReceive		: ReceiveString;
	sReceivedString	: STRING;
	sLastReceivedString: STRING;
	bStringReceived	: BOOL;
	bReceiveBusy	: BOOL;
	bReceiveError	: BOOL;
	eReceiveErrorID	: ComError_t;
	bReceiveTimeout	: BOOL;
	nReceiveCounter	: UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// Call the send block every second to repeat the transmission.
// Also call the block as long as it is busy to finish a transmission.
Timer(IN:=TRUE);
IF Timer.Q OR fbSend.Busy THEN
	Timer(IN:=FALSE); (* reset timer *)
	fbSend(	SendString:= '$02Hello World ! $03',
			TXbuffer:= TxBuffer,
			Busy=> bSendBusy,
			Error=> );
	IF fbSend.Error <> COMERROR_NOERROR THEN
		eSendErrorID := fbSend.Error;
	END_IF
END_IF

//	Receive string data:
//	The block receives any data strings beginning with a STX ($02) and ending with an ETX ($03) character.	
fbReceive(
	Prefix:= '$02',
	Suffix:= '$03',
	Timeout:= T#1s,
	ReceivedString:= sReceivedString,
	RXbuffer:= RxBuffer,
	StringReceived=> bStringReceived,
	Busy=> bReceiveBusy,
	Error=> ,
	RxTimeout=> bReceiveTimeout );
IF fbReceive.Error <> COMERROR_NOERROR THEN
	eReceiveErrorID := fbReceive.Error;
END_IF
IF bStringReceived THEN
	nReceiveCounter := nReceiveCounter + 1;
	sLastReceivedString := sReceivedString;
END_IF
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>